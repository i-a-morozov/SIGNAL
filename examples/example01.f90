! EXAMPLE-01: FREQUENCY ESTIMATION (SIGNAL WITH ONE HARMONIC)
PROGRAM EXAMPLE

  USE SIGNAL

  IMPLICIT NONE

  INTEGER(IK), PARAMETER           :: LENGTH = 2_IK**10_IK  ! INPUT SIGNAL LENGTH
  INTEGER(IK)                      :: FLAG                  ! COMPLEX FLAG (0/1)
  INTEGER(IK)                      :: PEAK                  ! FOURIE SPECTRA PEAK ID
  INTEGER(IK)                      :: METHOD                ! FREQUENCY ESTIMATION METHOD
  INTEGER(IK)                      :: ORDER                 ! COSINE WINDOW ORDER
  REAL(RK), DIMENSION(LENGTH)      :: WINDOW                ! COSINE WINDOW DATA
  REAL(RK)                         :: TOTAL                 ! SUM OF WINDOW ELEMENTS
  REAL(RK)                         :: FREQUENCY             ! EXACT SIGNAL FREQUENCY
  REAL(RK), DIMENSION(LENGTH)      :: SIGNAL_R, SIGNAL_I    ! SIGNAL REAL AND COMPLEX PARTS
  REAL(RK), DIMENSION(2_IK*LENGTH) :: SIGNAL                ! INPUT SIGNAL, [..., SR_I, SI_I, ...]
  REAL(RK), DIMENSION(2_IK*LENGTH) :: COPY                  ! INPUT SIGNAL COPY
  REAL(RK)                         :: RESULT                ! FREQUENCY ESTIMATION

  INTEGER(IK)                      :: I

  ! SET COMPLEX FLAG (0/1 FOR REAL/COMPLEX SIGNAL)
  ! FLAG IS USED BY FREQUENCY_ FUNCTION
  ! FOR REAL SIGNALS (FLAG = 0) 1ST HALF OF FOURIE AMPLITUDE SPETRUM IS USED FOR FREQUENCY ESTIMATION
  ! FREQUENCY RESOLUTION IS [0.0,0.5]
  ! FOR COMPLEX SIGNALS (FLAG = 1) FULL FOURIE AMPLITUDE SPECTRA IS USED FOR FREQUENCY ESTIMATION
  ! FREQUENCY RESOLUTION IS [0.0,1.0]
  FLAG = 1_IK

  ! SET SIGNAL EXACT FREQUENCY, REAL AND IMAGINARY PARTS
  FREQUENCY = 0.2143658791_RK
  DO I = 1_IK, LENGTH, 1_IK
    SIGNAL_R(I) = +COS(TWO_PI*FREQUENCY*REAL(I, RK))
    SIGNAL_I(I) = -SIN(TWO_PI*FREQUENCY*REAL(I, RK))
  END DO

  ! ADD CONSTANT COMPONENT
  SIGNAL_R = SIGNAL_R + 0.1_RK

  ! FORMAT TEST SIGNAL
  ! SIGNAL_R = [..., SR_I, ...]
  ! SIGNAL_I = [..., SI_I, ...]
  ! SIGNAL = [..., SR_I, SI_I, ...]
  SIGNAL = 0.0_RK
  IF(FLAG == 0_IK) THEN
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL)
  ELSE
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL_I, SIGNAL)
  END IF

  ! SET WINDOW AND WINDOW SUM
  ORDER = 2_IK
  CALL WINDOW_(LENGTH, ORDER, WINDOW)
  TOTAL = SUM(WINDOW)

  ! PRE-PROCESS SIGNAL (REMOVE WINDOW WEIGHTED MEAN)
  CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SIGNAL, COPY)
  SIGNAL = COPY

  ! APPLY WINDOW (REAL AND IMAGINARY PARTS)
  CALL APPLY_WINDOW(LENGTH, WINDOW, SIGNAL, COPY)
  SIGNAL = COPY

  ! IF SIGNAL LENGTH IS NOT EQUAL TO POWER OF TWO
  ! SIGNAL CAN BE PADDED WITH ZEROS TO THE NEAREST POWER OF TWO LENGTH
  ! IN THIS EXAMPLE SIGNAL HAS CORRECT LENGTH

  ! ROUND_UP_(LENGTH) RETURNS NEXT POWER OF TWO LENGTH
  WRITE(*, *) "LENGTH         : ", LENGTH
  WRITE(*, *) "NEXT LENGTH    : ", ROUND_UP_(LENGTH)

  ! ESTIMATE FREQUENCY
  ! SIGNAL SHOULD HAVE POWER OF TWO LENGTH (REAL AND IMAGINARY PART)
  ! PRE-PROCESSING SHOULD BE PERFORMED BEFORE FREQUENCY ESTIMATION
  ! WINDOW SHOULD BE APPLIED BEFORE ZERO PADDING

  ! PEAK = 0 USES LARGEST FOURIER AMPLITUDE SPECTRA BIN
  ! PEAK = N USES N'TH FOURIER AMPLITUDE SPECTRA PEAK
  ! FFT IS DEFINED BY __FFT__ DIRECTIVE
  ! FFT_RADIX_TWO_    -- RADIX-TWO FFT (NRF77), THREADSAFE
  ! FFT_RADIX_EIGHT_  -- RADIX-EIGHT FFT (TAKUYA OOURA), DEFAULT, THREADSAFE
  ! FFT_EXTERNAL_     -- FFTW, NOT THREADSAFE

  WRITE(*, *) "FLAG           : ", FLAG
  WRITE(*, *) "ORDER          : ", ORDER
  WRITE(*, *)

  ! ESTIMATE FREQUENCY (MAXIMUM BIN)
  WRITE(*, *) "MAXIMUM BIN    : "
  PEAK = 0_IK
  METHOD = FREQUENCY_FFT
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "FFT            : ", RESULT, ABS(RESULT-FREQUENCY)
  METHOD = FREQUENCY_FFRFT
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "FFRFT          : ", RESULT, ABS(RESULT-FREQUENCY)
  METHOD = FREQUENCY_PARABOLA
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "PARABOLA       : ", RESULT, ABS(RESULT-FREQUENCY)
  WRITE(*, *)

  ! IF PEAK \= 0, PEAK DETECTION IS USED TO LOCATE SELECTED PEAK
  ! PEAKS ARE SORTED BY __SORT__
  ! SORT_BUBBLE_      -- BUBBLE SORT
  ! SORT_QUICK_       -- QUICK SORT, DEFAULT

  ! ESTIMATE FREQUENCY (FIRST PEAK)
  WRITE(*, *) "FIRST PEAK     : "
  PEAK = 1_IK
  METHOD = FREQUENCY_FFT
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "FFT            : ", RESULT, ABS(RESULT-FREQUENCY)
  METHOD = FREQUENCY_FFRFT
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "FFRFT          : ", RESULT, ABS(RESULT-FREQUENCY)
  METHOD = FREQUENCY_PARABOLA
  RESULT = FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SIGNAL)
  WRITE(*, *) "PARABOLA       : ", RESULT, ABS(RESULT-FREQUENCY)
  WRITE(*, *)

  ! BETTER INITIAL FREQUENCY GUESS CAN BE OBTAINED WITH ZERO PADDING
  BLOCK
    REAL(RK), DIMENSION(2_IK*2_IK**12_IK) :: SEQUENCE
    CALL PAD_(LENGTH, 2_IK**12_IK, SIGNAL, SEQUENCE)
    WRITE(*, *) "ZERO PADDING   : "
    PEAK = 0_IK
    METHOD = FREQUENCY_FFT
    RESULT = FREQUENCY_(FLAG, PEAK, METHOD, SIZE(SEQUENCE)/2_IK, SEQUENCE)
    WRITE(*, *) "FFT            : ", RESULT, ABS(RESULT-FREQUENCY)
    METHOD = FREQUENCY_FFRFT
    RESULT = FREQUENCY_(FLAG, PEAK, METHOD, SIZE(SEQUENCE)/2_IK, SEQUENCE)
    WRITE(*, *) "FFRFT          : ", RESULT, ABS(RESULT-FREQUENCY)
    METHOD = FREQUENCY_PARABOLA
    RESULT = FREQUENCY_(FLAG, PEAK, METHOD, SIZE(SEQUENCE)/2_IK, SEQUENCE)
    WRITE(*, *) "PARABOLA       : ", RESULT, ABS(RESULT-FREQUENCY)
    WRITE(*, *)
  END BLOCK

  ! RESTORE ORIGINAL SIGNAL
  SIGNAL = 0.0_RK
  IF(FLAG == 0_IK) THEN
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL)
  ELSE
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL_I, SIGNAL)
  END IF

  ! INITIAL FREQUENCY ESTIMATION
  WRITE(*, *) "INITIAL GUESS  : "
  ! INITIAL FREQUENCY ESTIMATION (DFT)
  BLOCK
    PEAK = 0_IK
    METHOD = FREQUENCY_FFT
    RESULT = FREQUENCY_INITIAL_(FLAG, PEAK, METHOD, LENGTH, LENGTH, TOTAL, WINDOW, SIGNAL)
    WRITE(*, *) "DFT            : ", RESULT, ABS(RESULT-FREQUENCY)
  END BLOCK
  ! INITIAL FREQUENCY ESTIMATION (DFT/PAD)
  BLOCK
    PEAK = 0_IK
    METHOD = FREQUENCY_FFT
    RESULT = FREQUENCY_INITIAL_(FLAG, PEAK, METHOD, LENGTH, LENGTH*2_IK**6_IK, TOTAL, WINDOW, SIGNAL)
    WRITE(*, *) "DFT/PAD        : ", RESULT, ABS(RESULT-FREQUENCY)
  END BLOCK
  WRITE(*, *)

  ! REFINE INITIAL GUESS
  WRITE(*, *) "REFINED GUESS  : "
  ! REFINE WITH SEARCH (BINARY/GOLDEN) FUNCTIONS
  ! MAXIMUM AMPLITUDE SEARCH IS PERFORMED WITH DTFT
  BLOCK
    REAL(RK) :: GUESS
    REAL(RK) :: INTERVAL
    INTEGER(IK) :: LIMIT
    REAL(RK) :: TOLERANCE
    GUESS = FREQUENCY_INITIAL_(FLAG, PEAK, METHOD, LENGTH, LENGTH*2_IK**6_IK, TOTAL, WINDOW, SIGNAL)
    INTERVAL = 2.0_RK/REAL(LENGTH,RK)
    LIMIT = 100_IK
    TOLERANCE = 1.0E-16_RK
    RESULT = BINARY_AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SIGNAL, GUESS, INTERVAL, LIMIT, TOLERANCE)
    WRITE(*, *) "BINARY         : ", RESULT, ABS(RESULT-FREQUENCY)
    RESULT = GOLDEN_AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SIGNAL, GUESS, INTERVAL, LIMIT, TOLERANCE)
    WRITE(*, *) "GOLDEN         : ", RESULT, ABS(RESULT-FREQUENCY)
  END BLOCK
  WRITE(*, *)

END PROGRAM EXAMPLE