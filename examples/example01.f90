! gfortran -c -cpp -std=f2018 -Wall -pedantic -O3 -ffast-math -march=native -Wno-unused-function signal.f90
! ar rcs libsignal.a signal.o
! gfortran -o example01 -cpp -std=f2018 -Wall -pedantic -O3 -ffast-math -march=native -L. example01.f90 -lsignal  -llapack -lblas -lm -lfftw3 -lgsl -lgslcblas -lgfortran

! EXAMPLE-01: FREQUENCY ESTIMATION (SIGNAL WITH ONE HARMONIC)
PROGRAM EXAMPLE

  USE SIGNAL

  IMPLICIT NONE

  INTEGER(IK), PARAMETER           :: LENGTH = 2_IK**10_IK  ! INPUT SIGNAL LENGTH
  INTEGER(IK)                      :: FLAG                  ! COMPLEX FLAG (0/1)
  INTEGER(IK)                      :: PEAK                  ! FOURIE SPECTRA PEAK ID
  INTEGER(IK)                      :: ORDER                 ! COSINE WINDOW ORDER
  REAL(RK), DIMENSION(LENGTH)      :: WINDOW                ! COSINE WINDOW DATA
  REAL(RK)                         :: TOTAL                 ! SUM OF WINDOW ELEMENTS
  REAL(RK)                         :: FREQUENCY             ! EXACT SIGNAL FREQUENCY
  REAL(RK), DIMENSION(LENGTH)      :: SIGNAL_R, SIGNAL_I    ! SIGNAL REAL AND COMPLEX PARTS
  REAL(RK), DIMENSION(2_IK*LENGTH) :: SIGNAL                ! INPUT SIGNAL, [..., SR_I, SI_I, ...]
  REAL(RK)                         :: RESULT                ! FREQUENCY ESTIMATION

  INTEGER(IK)                      :: I

  WRITE(*, *) IK_SIZE
  WRITE(*, *) RK_SIZE

  ! SET COMPLEX FLAG (0/1 FOR REAL/COMPLEX SIGNAL)
  ! FLAG IS USED BY FREQUENCY_ FUNCTION
  ! FOR REAL SIGNALS (FLAG = 0) 1ST HALF OF FOURIE AMPLITUDE SPETRUM IS USED FOR FREQUENCY ESTIMATION
  ! FREQUENCY RESOLUTION IS [0.0,0.5]
  ! FOR COMPLEX SIGNALS (FLAG = 1) FULL FOURIE AMPLITUDE SPECTRA IS USED FOR FREQUENCY ESTIMATION
  ! FREQUENCY RESOLUTION IS [0.0,1.0]
  FLAG = 0_IK

  ! SET SIGNAL EXACT FREQUENCY, REAL AND IMAGINARY PARTS
  FREQUENCY = 0.2143658791_RK
  DO I = 1_IK, LENGTH, 1_IK
    SIGNAL_R(I) = +COS(TWO_PI*FREQUENCY*REAL(I, RK))
    SIGNAL_I(I) = -SIN(TWO_PI*FREQUENCY*REAL(I, RK))
  END DO

  ! FORMAT TEST SIGNAL
  ! SIGNAL_R = [..., SR_I, ...]
  ! SIGNAL_I = [..., SI_I, ...]
  ! SIGNAL = [..., SR_I, SI_I, ...]
  SIGNAL = 0.0_RK
  IF(FLAG == 0_IK) THEN
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL)
  ELSE
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL_I, SIGNAL)
  END IF

  ! SET WINDOW AND WINDOW SUM
  ORDER = 2_IK
  CALL WINDOW_(LENGTH, ORDER, WINDOW)
  TOTAL = SUM(WINDOW)

  ! ESTIMATE FREQUENCY

  ! PEAK = 0 USES LARGEST FOURIER AMPLITUDE SPECTRA BIN
  ! PEAK = N USES N'TH FOURIER AMPLITUDE SPECTRA PEAK
  ! IF PEAK < 0, FREQUENCY IS ESTIMATED ONLY FROM FFT WITHOUT INTERPOLATION (MAXIMUM BIN)
  ! ELSE, FREQUENCY IS ESTIMATED FROM INTERPOLATED FFT
  ! FFT IS DEFINED BY __FFT__ DIRECTIVE
  ! FFT_RADIX_TWO_    -- RADIX-TWO FFT (NRF77), THREADSAFE
  ! FFT_RADIX_EIGHT_  -- RADIX-EIGHT FFT (TAKUYA OOURA), DEFAULT, THREADSAFE
  ! FFT_EXTERNAL_     -- FFTW, NOT THREADSAFE

  WRITE(*, *) "LENGTH         : ", LENGTH
  WRITE(*, *) "FLAG           : ", FLAG
  WRITE(*, *) "ORDER          : ", ORDER
  WRITE(*, *)

  ! ESTIMATE FREQUENCY (MAXIMUM BIN)
  PEAK = 0_IK
  RESULT = FREQUENCY_(FLAG, PEAK, LENGTH, TOTAL, WINDOW, SIGNAL)
  WRITE(*, *) "LARGEST BIN    : ", RESULT, ABS(RESULT-FREQUENCY)

  ! ESTIMATE FREQUENCY (1ST PEAK, INTERPOLATED FFT APPROXIMATION)
  PEAK = +1_IK
  RESULT = FREQUENCY_(FLAG, PEAK, LENGTH, TOTAL, WINDOW, SIGNAL)
  WRITE(*, *) "1ST PEAK       : ", RESULT, ABS(RESULT-FREQUENCY)

  ! ESTIMATE FREQUENCY (1ST PEAK, FFT APPROXIMATION)
  PEAK = -1_IK
  RESULT = FREQUENCY_(FLAG, PEAK, LENGTH, TOTAL, WINDOW, SIGNAL)
  WRITE(*, *) "1ST PEAK (FFT) : ", RESULT, ABS(RESULT-FREQUENCY)

  ! IF PEAK \= 0, PEAK DETECTION IS USED TO LOCATE SELECTED PEAK
  ! PEAKS ARE SORTED BY __SORT__
  ! SORT_BUBBLE_      -- BUBBLE SORT
  ! SORT_QUICK_       -- QUICK SORT, DEFAULT

END PROGRAM EXAMPLE