! gfortran -c -cpp -std=f2018 -Wall -pedantic -O3 -ffast-math -march=native -Wno-unused-function signal.f90
! ar rcs libsignal.a signal.o
! gfortran -o example03 -cpp -std=f2018 -Wall -pedantic -O3 -ffast-math -march=native -L. example03.f90 -lsignal -llapack -lblas -lm -lfftw3 -lgsl -lgslcblas -lgfortran

! EXAMPLE-03: HARMONIC SIGNAL DECOMPOSITION
PROGRAM EXAMPLE

  USE SIGNAL

  IMPLICIT NONE

  INTEGER(IK), PARAMETER           :: LENGTH = 2_IK**12_IK  ! INPUT SIGNAL LENGTH
  INTEGER(IK)                      :: FLAG                  ! COMPLEX FLAG (0/1)
  INTEGER(IK)                      :: ORDER                 ! COSINE WINDOW ORDER
  REAL(RK), DIMENSION(LENGTH)      :: WINDOW                ! COSINE WINDOW DATA
  REAL(RK)                         :: TOTAL                 ! SUM OF WINDOW ELEMENTS
  REAL(RK), DIMENSION(LENGTH)      :: SIGNAL_R, SIGNAL_I    ! SIGNAL REAL AND COMPLEX PARTS
  REAL(RK), DIMENSION(2_IK*LENGTH) :: SIGNAL                ! INPUT SIGNAL, [..., SR_I, SI_I, ...]
  INTEGER(IK), PARAMETER           :: LOOP = 4_IK           ! NUMBER OF HARMONICS
  REAL(RK), DIMENSION(LOOP)        :: FREQUENCY             ! FREQUENCIES 
  REAL(RK)                         :: MEAN                  ! SINGAL MEAN
  REAL(RK), DIMENSION(LOOP)        :: COS_AMP               ! COS-AMPLITUDES
  REAL(RK), DIMENSION(LOOP)        :: SIN_AMP               ! SIN-AMPLITUDES

  REAL(RK), PARAMETER              :: F1 = 1._RK*0.123456789_RK
  REAL(RK), PARAMETER              :: F2 = 2._RK*0.123456789_RK
  REAL(RK), PARAMETER              :: F3 = 3._RK*0.123456789_RK
  REAL(RK), PARAMETER              :: F4 = 4._RK*0.123456789_RK
  REAL(RK), PARAMETER              :: A1 = 1.0_RK
  REAL(RK), PARAMETER              :: B1 = 1.0E-1_RK
  REAL(RK), PARAMETER              :: A2 = 1.0E-1_RK
  REAL(RK), PARAMETER              :: B2 = 5.0E-2_RK
  REAL(RK), PARAMETER              :: A3 = 1.E-3_RK
  REAL(RK), PARAMETER              :: B3 = 0.0_RK
  REAL(RK), PARAMETER              :: A4 = 0.0_RK
  REAL(RK), PARAMETER              :: B4 = 1.E-4_RK

  INTEGER(IK)                      :: I
  REAL(RK)                         :: ERROR

  ! SET COMPLEX FLAG (0/1 FOR REAL/COMPLEX SIGNAL)
  FLAG = 1_IK

  ! SET SIGNAL REAL AND IMAGINARY PARTS
  SIGNAL_R = 1.0_RK
  SIGNAL_I = 0.0_RK
  DO I = 1_IK, LENGTH, 1_IK
    SIGNAL_R(I) = &
      A1*COS(TWO_PI*F1*REAL(I, RK)) + B1*SIN(TWO_PI*F1*REAL(I, RK)) + &
      A2*COS(TWO_PI*F2*REAL(I, RK)) + B2*SIN(TWO_PI*F2*REAL(I, RK)) + &
      A3*COS(TWO_PI*F3*REAL(I, RK)) + B3*SIN(TWO_PI*F3*REAL(I, RK)) + &
      A4*COS(TWO_PI*F4*REAL(I, RK)) + B4*SIN(TWO_PI*F4*REAL(I, RK))
    SIGNAL_I(I) = &
      B1*COS(TWO_PI*F1*REAL(I, RK)) - A1*SIN(TWO_PI*F1*REAL(I, RK)) + &
      B2*COS(TWO_PI*F2*REAL(I, RK)) - A2*SIN(TWO_PI*F2*REAL(I, RK)) + &
      B3*COS(TWO_PI*F3*REAL(I, RK)) - A3*SIN(TWO_PI*F3*REAL(I, RK)) + &
      B4*COS(TWO_PI*F4*REAL(I, RK)) - A4*SIN(TWO_PI*F4*REAL(I, RK))
  END DO

  ! FORMAT TEST SIGNAL
  SIGNAL = 0.0_RK
  IF(FLAG == 0_IK) THEN
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL)
  ELSE
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL_I, SIGNAL)
  END IF

  ! SET WINDOW AND WINDOW SUM
  ORDER = 2_IK
  CALL WINDOW_(LENGTH, ORDER, WINDOW)
  TOTAL = SUM(WINDOW)

  ! PRINT INPUT SIGNAL PARAMETERS
  WRITE(*, *) "INPUT"
  WRITE(*, *) F1, A1, B1
  WRITE(*, *) F2, A2, B2
  WRITE(*, *) F3, A3, B3
  WRITE(*, *) F4, A4, B4
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES
  WRITE(*, *) "FREQUENCY (PEAKS)"
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY_(FLAG, I, LENGTH, TOTAL, WINDOW, SIGNAL)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING PEAKS
  ! ALL FREQUENCIES ARE ESTIMATED FROM INPUT SIGNAL
  WRITE(*, *) "DECOMPOSITION (PEAKS)"
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL DECOMPOSITION_(FLAG, DECOMPOSITION_PEAK, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  DO I=1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I), COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES USING MAXIMUM BIN AND SUBTRACTION
  ! ALL FREQUENCIES ARE ESTIMATED FROM INPUT SIGNAL
  ! AMPLITUDES ARE COMPUTED, BUT ONLY FREQUENCIES ARE RETURNED 
  WRITE(*, *) "FREQUENCY LIST (PEAKS)"
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FREQUENCY_LIST_(FLAG, DECOMPOSITION_PEAK, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING MAXIMUM BIN AND SUBTRACTION
  ! FREQUENCIES ARE ESTIMATED FROM LARGEST BIN
  WRITE(*, *) "DECOMPOSITION (SUBTRACTION)"
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL DECOMPOSITION_(FLAG, DECOMPOSITION_MAX, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I), COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING MAXIMUM BIN AND SUBTRACTION
  ! FREQUENCIES ARE ESTIMATED FROM LARGEST BIN
  ! AMPLITUDES ARE COMPUTED, BUT ONLY FREQUENCIES ARE RETURNED
  WRITE(*, *) "FREQUENCY LIST (SUBTRACTION)"
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FREQUENCY_LIST_(FLAG, 0_IK, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE AMPLITUDES FOR KNOWN FREQUENCIES (FOURIER)
  WRITE(*, *) "AMPLITUDES (FOURIER)"
  MEAN = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  MEAN = SUM(SIGNAL_R)/REAL(LENGTH,RK)
  CALL AMPLITUDE_LIST_(FLAG, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  WRITE(*, *) MEAN
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE AMPLITUDES FOR KNOWN FREQUENCIES (LEAST SQUARES)
  WRITE(*, *) "AMPLITUDES (LEAST SQUARES)"
  MEAN = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FIT_(128_IK, SIGNAL_R(1_IK : 128_IK), LOOP, FREQUENCY, MEAN, COS_AMP, SIN_AMP, ERROR)
  WRITE(*, *) MEAN
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! COMPUTE AMPLITUDES FOR KNOWN FREQUENCY
  WRITE(*, *) "AMPLITUDES (STANDALONE FOURIER)"
  BLOCK
    REAL(RK) :: AMP_COS, AMP_SIN, AMP
    DO I = 1_IK, LOOP, 1_IK
      CALL AMPLITUDE_(LENGTH, TOTAL, WINDOW, SIGNAL, FREQUENCY(I), AMP_COS, AMP_SIN, AMP)
      WRITE(*, *) AMP_COS, AMP_SIN, AMP
    END DO
  END BLOCK
  WRITE(*, *)

END PROGRAM EXAMPLE