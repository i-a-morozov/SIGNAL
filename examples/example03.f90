! EXAMPLE-03: HARMONIC SIGNAL DECOMPOSITION
PROGRAM EXAMPLE

  USE SIGNAL

  IMPLICIT NONE

  INTEGER(IK), PARAMETER           :: LENGTH = 2_IK**10_IK  ! INPUT SIGNAL LENGTH
  INTEGER(IK)                      :: FLAG                  ! COMPLEX FLAG (0/1)
  INTEGER(IK)                      :: METHOD                ! FREQUENCY ESTIMATION METHOD
  INTEGER(IK)                      :: MODE                  ! DECOMPOSITION MODE
  INTEGER(IK)                      :: ORDER                 ! COSINE WINDOW ORDER
  REAL(RK), DIMENSION(LENGTH)      :: WINDOW                ! COSINE WINDOW DATA
  REAL(RK)                         :: TOTAL                 ! SUM OF WINDOW ELEMENTS
  REAL(RK), DIMENSION(LENGTH)      :: SIGNAL_R, SIGNAL_I    ! SIGNAL REAL AND COMPLEX PARTS
  REAL(RK), DIMENSION(2_IK*LENGTH) :: SIGNAL                ! INPUT SIGNAL, [..., SR_I, SI_I, ...]
  INTEGER(IK), PARAMETER           :: LOOP = 4_IK           ! NUMBER OF HARMONICS
  REAL(RK), DIMENSION(LOOP)        :: FREQUENCY             ! FREQUENCIES
  REAL(RK)                         :: MEAN                  ! SINGAL MEAN
  REAL(RK), DIMENSION(LOOP)        :: COS_AMP               ! COS-AMPLITUDES
  REAL(RK), DIMENSION(LOOP)        :: SIN_AMP               ! SIN-AMPLITUDES

  REAL(RK), PARAMETER              :: F(4) = [1._RK*0.123456789_RK,2._RK*0.123456789_RK,3._RK*0.123456789_RK,4._RK*0.123456789_RK]
  REAL(RK), PARAMETER              :: A(4) = [1.0_RK,1.0E-1_RK,1.E-3_RK,0.0_RK]
  REAL(RK), PARAMETER              :: B(4) = [1.0E-1_RK,5.0E-2_RK,0.0_RK,1.E-4_RK]

  INTEGER(IK)                      :: I
  REAL(RK)                         :: ERROR

  ! SET COMPLEX FLAG (0/1 FOR REAL/COMPLEX SIGNAL)
  FLAG = 1_IK

  ! SET SIGNAL REAL AND IMAGINARY PARTS
  SIGNAL_R = 1.0_RK
  SIGNAL_I = 0.0_RK
  DO I = 1_IK, LENGTH, 1_IK
    SIGNAL_R(I) = &
      A(1)*COS(TWO_PI*F(1)*REAL(I, RK)) + B(1)*SIN(TWO_PI*F(1)*REAL(I, RK)) + &
      A(2)*COS(TWO_PI*F(2)*REAL(I, RK)) + B(2)*SIN(TWO_PI*F(2)*REAL(I, RK)) + &
      A(3)*COS(TWO_PI*F(3)*REAL(I, RK)) + B(3)*SIN(TWO_PI*F(3)*REAL(I, RK)) + &
      A(4)*COS(TWO_PI*F(4)*REAL(I, RK)) + B(4)*SIN(TWO_PI*F(4)*REAL(I, RK))
    SIGNAL_I(I) = &
      B(1)*COS(TWO_PI*F(1)*REAL(I, RK)) - A(1)*SIN(TWO_PI*F(1)*REAL(I, RK)) + &
      B(2)*COS(TWO_PI*F(2)*REAL(I, RK)) - A(2)*SIN(TWO_PI*F(2)*REAL(I, RK)) + &
      B(3)*COS(TWO_PI*F(3)*REAL(I, RK)) - A(3)*SIN(TWO_PI*F(3)*REAL(I, RK)) + &
      B(4)*COS(TWO_PI*F(4)*REAL(I, RK)) - A(4)*SIN(TWO_PI*F(4)*REAL(I, RK))
  END DO

  ! FORMAT TEST SIGNAL
  SIGNAL = 0.0_RK
  IF(FLAG == 0_IK) THEN
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL)
  ELSE
    CALL CONVERT_(LENGTH, SIGNAL_R, SIGNAL_I, SIGNAL)
  END IF

  ! SET WINDOW AND WINDOW SUM
  ORDER = 4_IK
  CALL WINDOW_(LENGTH, ORDER, WINDOW)
  TOTAL = SUM(WINDOW)

  ! PRINT INPUT SIGNAL PARAMETERS
  WRITE(*, *) "INPUT"
  WRITE(*, *) F(1), A(1), B(1)
  WRITE(*, *) F(2), A(2), B(2)
  WRITE(*, *) F(3), A(3), B(3)
  WRITE(*, *) F(4), A(4), B(4)
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES
  WRITE(*, *) "FREQUENCY (PEAKS)"
  BLOCK
    REAL(RK), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK) :: RESULT
    CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SIGNAL, SEQUENCE)
    SEQUENCE(1_IK::2_IK) = SEQUENCE(1_IK::2_IK)*WINDOW
    SEQUENCE(2_IK::2_IK) = SEQUENCE(2_IK::2_IK)*WINDOW
    DO I = 1_IK, LOOP, 1_IK
      RESULT = FREQUENCY_(FLAG, I, FREQUENCY_PARABOLA, LENGTH, SEQUENCE)
      WRITE(*, *) RESULT
    END DO
  END BLOCK
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING PEAKS
  ! ALL FREQUENCIES ARE ESTIMATED FROM INPUT SIGNAL
  WRITE(*, *) "DECOMPOSITION (PEAKS)"
  METHOD = FREQUENCY_PARABOLA
  MODE = DECOMPOSITION_PEAK
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL DECOMPOSITION_(FLAG, METHOD, MODE, LENGTH, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  DO I=1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I), COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES USING PEAKS
  ! ALL FREQUENCIES ARE ESTIMATED FROM INPUT SIGNAL
  ! AMPLITUDES ARE COMPUTED, BUT ONLY FREQUENCIES ARE RETURNED
  WRITE(*, *) "FREQUENCY LIST (PEAKS)"
  METHOD = FREQUENCY_PARABOLA
  MODE = DECOMPOSITION_PEAK
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FREQUENCY_LIST_(FLAG, METHOD, MODE, LENGTH, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING MAXIMUM BIN AND SUBTRACTION
  ! FREQUENCIES ARE ESTIMATED FROM LARGEST BIN
  WRITE(*, *) "DECOMPOSITION (SUBTRACTION)"
  METHOD = FREQUENCY_PARABOLA
  MODE = DECOMPOSITION_SUBTRACT
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL DECOMPOSITION_(FLAG, METHOD, MODE, LENGTH, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I), COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE FREQUENCIES AND AMPLITUDES USING MAXIMUM BIN AND SUBTRACTION
  ! FREQUENCIES ARE ESTIMATED FROM LARGEST BIN
  ! AMPLITUDES ARE COMPUTED, BUT ONLY FREQUENCIES ARE RETURNED
  WRITE(*, *) "FREQUENCY LIST (SUBTRACTION)"
  METHOD = FREQUENCY_PARABOLA
  MODE = DECOMPOSITION_SUBTRACT
  FREQUENCY = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FREQUENCY_LIST_(FLAG, METHOD, MODE, LENGTH, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY)
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) FREQUENCY(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE AMPLITUDES FOR KNOWN FREQUENCIES (FOURIER)
  WRITE(*, *) "AMPLITUDES (FOURIER)"
  MEAN = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  MEAN = SUM(SIGNAL_R)/REAL(LENGTH,RK)
  CALL AMPLITUDE_LIST_(FLAG, LENGTH, TOTAL, WINDOW, SIGNAL, LOOP, FREQUENCY, COS_AMP, SIN_AMP)
  WRITE(*, *) MEAN
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! ESTIMATE AMPLITUDES FOR KNOWN FREQUENCIES (LEAST SQUARES)
  WRITE(*, *) "AMPLITUDES (LEAST SQUARES)"
  MEAN = 0.0_RK
  COS_AMP = 0.0_RK
  SIN_AMP = 0.0_RK
  CALL FIT_(256_IK, SIGNAL_R(1_IK : 256_IK), LOOP, FREQUENCY, MEAN, COS_AMP, SIN_AMP, ERROR)
  WRITE(*, *) MEAN
  DO I = 1_IK, LOOP, 1_IK
    WRITE(*, *) COS_AMP(I), SIN_AMP(I)
  END DO
  WRITE(*, *)

  ! COMPUTE AMPLITUDES FOR KNOWN FREQUENCY
  WRITE(*, *) "AMPLITUDES (STANDALONE FOURIER)"
  BLOCK
    REAL(RK) :: AMP_COS, AMP_SIN, AMP
    DO I = 1_IK, LOOP, 1_IK
      CALL AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SIGNAL, FREQUENCY(I), AMP_COS, AMP_SIN, AMP)
      WRITE(*, *) AMP_COS, AMP_SIN, AMP
    END DO
  END BLOCK
  WRITE(*, *)

END PROGRAM EXAMPLE