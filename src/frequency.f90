
#include "signal.inc"

SUBMODULE (SIGNAL) FREQUENCY
  IMPLICIT NONE
  CONTAINS
  ! ############################################################################################################################# !
  ! FREQUENCY ESTIMATION
  ! (FUNCTION) FREQUENCY_(<FLAG>, <PEAK>, <METHOD>, <LENGTH>, <SEQUENCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = +N USE N'TH PEAK
  ! <METHOD>               -- (IN)     FREQUENCY APPROXIMATION METHOD (IK), FREQUENCY_FFT = 0_IK, FREQUENCY_FFRFT = 1_IK, FREQUECY_PARABOLA = 2_IK, FREQUECY_PARABOLA_FIT = 3_IK
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK), POWER OF TWO, NOT CHECKED
  ! <SEQUENCE>             -- (IN)     INPUT SEQUENCE (PROCESSED, I.E. WITH WINDOW AND PADDED WITH ZEROS) (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    FREQUENCY ESTIMATION (RK)
  ! double  frequency_(int*, int*, int*, int*, double*) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_(FLAG, PEAK, METHOD, LENGTH, SEQUENCE) &
    BIND(C, NAME = "frequency_")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN) :: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: FOURIER, LOCAL
    INTEGER(IK) :: FST, CND
    REAL(RK) :: FACTOR
    REAL(RK), DIMENSION(LENGTH) :: MUL, COS_MUL, SIN_MUL
    INTEGER(IK) :: I
    FREQUENCY_ = 0.0_RK
    ! FFT
    LOCAL = SEQUENCE
    FOURIER = SEQUENCE
    CALL __FFT__(LENGTH, FFT_FORWARD, FOURIER)
    MUL = LOG10(SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK)+1.E-16_RK)
    IF (PEAK == 0_IK) THEN
      FST = INT(__MAXLOC__(MUL(1_IK:LENGTH/(2_IK-FLAG):1_IK), 1_IK), IK)
    ELSE
      FST = PEAK_(LENGTH/(2_IK-FLAG), MUL(1_IK:LENGTH/(2_IK-FLAG):1_IK), ABS(PEAK))
    END IF
    IF (METHOD == FREQUENCY_FFT) THEN
      FREQUENCY_ = REAL(FST-1_IK, RK)/REAL(LENGTH, RK)
      RETURN
    END IF
    ! FFRFT
    FACTOR = TWO_PI*REAL(FST-2_IK, RK)/REAL(LENGTH, RK)
    MUL = FACTOR*REAL([(I, I = 0_IK, LENGTH-1_IK, 1_IK)], RK)
    COS_MUL = COS(MUL)
    SIN_MUL = SIN(MUL)
    FOURIER = LOCAL
    LOCAL(1_IK::2_IK) = FOURIER(1_IK::2_IK)*COS_MUL-FOURIER(2_IK::2_IK)*SIN_MUL
    LOCAL(2_IK::2_IK) = FOURIER(1_IK::2_IK)*SIN_MUL+FOURIER(2_IK::2_IK)*COS_MUL
    CALL FFRFT_(LENGTH, 2.0_RK/REAL(LENGTH, RK), LOCAL)
    MUL = LOG10(SQRT(LOCAL(1_IK::2_IK)**2_IK+LOCAL(2_IK::2_IK)**2_IK)+1.E-16_RK)
    CND = INT(__MAXLOC__(MUL, 1_IK), IK)
    IF (METHOD == FREQUENCY_FFRFT) THEN
      FREQUENCY_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(REAL(CND, RK)-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    ! PARABOLA
    IF (METHOD == FREQUENCY_PARABOLA) THEN
      FREQUENCY_ = REAL(CND, RK)-0.5_RK+(MUL(-1_IK+CND)-MUL(CND))/(MUL(-1_IK+CND)-2.0_RK*MUL(CND)+MUL(1_IK+CND))
      FREQUENCY_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA_FIT) THEN
      BLOCK
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: X
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: Y
        REAL(RK) :: A, B, C
        INTEGER(IK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: INDEX
        INDEX = CND + [(I, I = -PARABOLA_FIT_LENGTH, +PARABOLA_FIT_LENGTH, 1_IK)]
        X = REAL(INDEX, RK)
        Y = MUL([INDEX])
        CALL FIT_PARABOLA_(PARABOLA_FIT_LENGTH, X, Y, A, B, C, FREQUENCY_)
        FREQUENCY_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
        RETURN
      END BLOCK
    END IF
  END FUNCTION FREQUENCY_
  ! ############################################################################################################################# !
  ! FREQUENCY ESTIMATION (FFT AND FFRFT DATA MEMORIZATION)
  ! (FUNCTION) FREQUENCY__(<FLAG>, <PEAK>, <METHOD>, <LENGTH>, <SEQUENCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = +N USE N'TH PEAK
  ! <METHOD>               -- (IN)     FREQUENCY APPROXIMATION METHOD (IK), FREQUENCY_FFT = 0_IK, FREQUENCY_FFRFT = 1_IK, FREQUECY_PARABOLA = 2_IK, FREQUECY_PARABOLA_FIT = 3_IK
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK), POWER OF TWO, NOT CHECKED
  ! <SEQUENCE>             -- (IN)     INPUT SEQUENCE (PROCESSED, I.E. WITH WINDOW AND PADDED WITH ZEROS) (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    FREQUENCY ESTIMATION (RK)
  ! double  frequency_(int*, int*, int*, int*, double*) ;
  MODULE REAL(RK) FUNCTION FREQUENCY__(FLAG, PEAK, METHOD, LENGTH, SEQUENCE) &
    BIND(C, NAME = "frequency__")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN) :: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: FOURIER, LOCAL
    INTEGER(IK) :: FST, CND
    REAL(RK) :: FACTOR
    REAL(RK), DIMENSION(LENGTH) :: MUL, COS_MUL, SIN_MUL
    INTEGER(IK) :: I
    FREQUENCY__ = 0.0_RK
    ! FFT
    LOCAL = SEQUENCE
    FOURIER = SEQUENCE
    CALL FFT_RADIX_EIGHT__(LENGTH, FFT_FORWARD, FOURIER, BANK%BIT_FFT, BANK%TRIG_FFT)
    MUL = LOG10(SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK)+1.E-16_RK)
    IF (PEAK == 0_IK) THEN
      FST = INT(__MAXLOC__(MUL(1_IK:LENGTH/(2_IK-FLAG):1_IK), 1_IK), IK)
    ELSE
      FST = PEAK_(LENGTH/(2_IK-FLAG), MUL(1_IK:LENGTH/(2_IK-FLAG):1_IK), ABS(PEAK))
    END IF
    IF (METHOD == FREQUENCY_FFT) THEN
      FREQUENCY__ = REAL(FST-1_IK, RK)/REAL(LENGTH, RK)
      RETURN
    END IF
    ! FFRFT
    FACTOR = TWO_PI*REAL(FST-2_IK, RK)/REAL(LENGTH, RK)
    MUL = FACTOR*REAL([(I, I = 0_IK, LENGTH-1_IK, 1_IK)], RK)
    COS_MUL = COS(MUL)
    SIN_MUL = SIN(MUL)
    FOURIER = LOCAL
    LOCAL(1_IK::2_IK) = FOURIER(1_IK::2_IK)*COS_MUL-FOURIER(2_IK::2_IK)*SIN_MUL
    LOCAL(2_IK::2_IK) = FOURIER(1_IK::2_IK)*SIN_MUL+FOURIER(2_IK::2_IK)*COS_MUL
    CALL FFRFT__(LENGTH, LOCAL, BANK%BIT_FFRFT, BANK%TRIG_FFRFT, BANK%COS_FST, BANK%SIN_FST, BANK%COS_LST, BANK%SIN_LST)
    MUL = LOG10(SQRT(LOCAL(1_IK::2_IK)**2_IK+LOCAL(2_IK::2_IK)**2_IK)+1.E-16_RK)
    CND = INT(__MAXLOC__(MUL, 1), IK)
    IF (METHOD == FREQUENCY_FFRFT) THEN
      FREQUENCY__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(REAL(CND, RK)-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    ! PARABOLA
    IF (METHOD == FREQUENCY_PARABOLA) THEN
      FREQUENCY__ = REAL(CND, RK)-0.5_RK+(MUL(-1_IK+CND)-MUL(CND))/(MUL(-1_IK+CND)-2.0_RK*MUL(CND)+MUL(1_IK+CND))
      FREQUENCY__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY__-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA_FIT) THEN
      BLOCK
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: X
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: Y
        REAL(RK) :: A, B, C
        INTEGER(IK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: INDEX
        INDEX = CND + [(I, I = -PARABOLA_FIT_LENGTH, +PARABOLA_FIT_LENGTH, 1_IK)]
        X = REAL(INDEX, RK)
        Y = MUL([INDEX])
        CALL FIT_PARABOLA_(PARABOLA_FIT_LENGTH, X, Y, A, B, C, FREQUENCY__)
        FREQUENCY__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY__-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
        RETURN
      END BLOCK
    END IF
  END FUNCTION FREQUENCY__
  ! ############################################################################################################################# !
  ! INITIAL FREQUENCY ESTIMATION
  ! (FUNCTION) FREQUENCY_INITIAL_(<FLAG>, <PEAK>, <METHOD>, <LENGTH>, <SEQUENCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = +N USE N'TH PEAK
  ! <METHOD>               -- (IN)     INITIAL FREQUENCY APPROXIMATION METHOD (IK)
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK), POWER OF TWO, NOT CHECKED
  ! <PAD>                  -- (IN)     PADDED SEQUENCE LENGTH (IK), IF PAD > LENGTH, INPUT SEQUENCE IS PADDED, POWER OF TWO, NOT CHECKED
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT SEQUENCE (PROCESSED, I.E. WITH WINDOW AND PADDED WITH ZEROS) (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    FREQUENCY ESTIMATION (RK)
  ! double  frequency_initial_(int*, int*, int*, int*, int*, double*, double*, double*) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_INITIAL_(FLAG, PEAK, METHOD, LENGTH, PAD, TOTAL, WINDOW, SEQUENCE) &
    BIND(C, NAME = "frequency_initial_")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN) :: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    INTEGER(IK), INTENT(IN):: PAD
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: COPY
    REAL(RK), DIMENSION(2_IK*PAD) :: FOURIER
    INTEGER(IK) :: FST
    REAL(RK), DIMENSION(PAD) :: MUL
    IF (METHOD == INITIAL_FFT) THEN
      CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SEQUENCE, COPY)
      COPY(1_IK::2_IK) = COPY(1_IK::2_IK)*WINDOW
      COPY(2_IK::2_IK) = COPY(2_IK::2_IK)*WINDOW
      CALL PAD_(LENGTH, PAD, COPY, FOURIER)
      CALL __FFT__(PAD, FFT_FORWARD, FOURIER)
      MUL = LOG10(SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK)+1.E-16_RK)
      IF (PEAK == 0_IK) THEN
        FST = INT(__MAXLOC__(MUL(1_IK:PAD/(2_IK-FLAG):1_IK), 1_IK), IK)
      ELSE
        FST = PEAK_(PAD/(2_IK-FLAG), MUL(1_IK:PAD/(2_IK-FLAG):1_IK), ABS(PEAK))
      END IF
      FREQUENCY_INITIAL_ = REAL(FST-1_IK, RK)/REAL(PAD, RK)
      RETURN
    END IF
    FREQUENCY_INITIAL_ = 0.0_RK
  END FUNCTION FREQUENCY_INITIAL_
  ! ############################################################################################################################# !
  ! REFINE FREQUENCY ESTIMATION (COMMON INTERFACE FOR FFRFT, PARABOLA AND SEARCH)
  ! (FUNCTION) FREQUENCY_REFINED_(<FLAG>, <LENGTH>, <TOTAL>, <WINDOW>, <SEQUENCE>, <GUESS>, <INTERVAL>, <LIMIT>, <TOLERANCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <METHOD>               -- (IN)     METHOD (SEE GLOBAL REFINED_* CONSTANTS)
  ! <LENGTH>               -- (IN)     SEQUENCE LENGTH (IK), POWER OF TWO, NOT CHECKED
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <GUESS>                -- (IN)     INITIAL GUESS VALUE (RK)
  ! <INTERVAL>             -- (IN)     SEARCH INTERVAL (RK), GUESS IS IN THE MIDLE
  ! <LIMIT>                -- (IN)     MAXIMUM NUMBER OF ITERATIONS (IK)
  ! <TOLERANCE>            -- (IN)     MAXIMUM TOLERANCE (RK)
  ! <FREQUENCY_REFINED_>   -- (OUT)    REFINED FREQUENCY
  ! double  frequenc_refined_(int*, int*, int*, double*, double*, double*, double*, double*, int*, double*) ;
  ! <METHOD> = REFINED_FFRFT           FFRFT
  ! <METHOD> = REFINED_PARABOLA        FFRFT & PARABOLA
  ! <METHOD> = REFINED_SEARCH          __SEARCH__
  MODULE REAL(RK) FUNCTION FREQUENCY_REFINED_(FLAG, METHOD, LENGTH, TOTAL, WINDOW, SEQUENCE, GUESS, INTERVAL, LIMIT, TOLERANCE) &
    BIND(C, NAME = "frequency_refined_")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN):: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), INTENT(IN) :: GUESS
    REAL(RK), INTENT(IN) :: INTERVAL
    INTEGER(IK), INTENT(IN) :: LIMIT
    REAL(RK), INTENT(IN) :: TOLERANCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: FOURIER, LOCAL
    INTEGER(IK) :: FST, CND
    REAL(RK) :: FACTOR
    REAL(RK), DIMENSION(LENGTH) :: MUL, COS_MUL, SIN_MUL
    INTEGER :: I
    FREQUENCY_REFINED_ = 0.0_RK
    IF (METHOD == REFINED_FFRFT .OR. METHOD == REFINED_PARABOLA .OR. METHOD == REFINED_PARABOLA_FIT) THEN
        FST = INT(REAL(LENGTH, RK)*GUESS, IK)+1_IK
        FACTOR = TWO_PI*REAL(FST-2_IK, RK)/REAL(LENGTH, RK)
        MUL = FACTOR*REAL([(I, I = 0_IK, LENGTH-1_IK, 1_IK)], RK)
        COS_MUL = COS(MUL)
        SIN_MUL = SIN(MUL)
        CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SEQUENCE, LOCAL)
        CALL APPLY_WINDOW_(LENGTH, WINDOW, LOCAL, FOURIER)
        LOCAL(1_IK::2_IK) = FOURIER(1_IK::2_IK)*COS_MUL-FOURIER(2_IK::2_IK)*SIN_MUL
        LOCAL(2_IK::2_IK) = FOURIER(1_IK::2_IK)*SIN_MUL+FOURIER(2_IK::2_IK)*COS_MUL
        CALL FFRFT_(LENGTH, 2.0_RK/REAL(LENGTH, RK), LOCAL)
        MUL = LOG10(SQRT(LOCAL(1_IK::2_IK)**2_IK+LOCAL(2_IK::2_IK)**2_IK)+1.E-16_RK)
        CND = INT(__MAXLOC__(MUL, 1_IK), IK)
        IF (METHOD == REFINED_FFRFT) THEN
          FREQUENCY_REFINED_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(REAL(CND, RK)-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
          RETURN
        END IF
        IF (METHOD == REFINED_PARABOLA) THEN
          FREQUENCY_REFINED_ = REAL(CND, RK)-0.5_RK+(MUL(-1_IK+CND)-MUL(CND))/(MUL(-1_IK+CND)-2.0_RK*MUL(CND)+MUL(1_IK+CND))
          FREQUENCY_REFINED_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINED_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
          RETURN
        END IF
        IF (METHOD == REFINED_PARABOLA_FIT) THEN
          BLOCK
            REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: X
            REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: Y
            REAL(RK) :: A, B, C
            INTEGER(IK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: INDEX
            INDEX = CND + [(I, I = -PARABOLA_FIT_LENGTH, +PARABOLA_FIT_LENGTH, 1_IK)]
            X = REAL(INDEX, RK)
            Y = MUL([INDEX])
            CALL FIT_PARABOLA_(PARABOLA_FIT_LENGTH, X, Y, A, B, C, FREQUENCY_REFINED_)
            FREQUENCY_REFINED_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINED_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
          END BLOCK
        END IF
    END IF
    IF (METHOD == REFINED_SEARCH) THEN
      FREQUENCY_REFINED_ = __SEARCH__(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, GUESS, INTERVAL, LIMIT, TOLERANCE)
    END IF
  END FUNCTION FREQUENCY_REFINED_
END SUBMODULE FREQUENCY