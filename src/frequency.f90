
#include "signal.inc"

SUBMODULE (SIGNAL) FREQUENCY
  IMPLICIT NONE
  CONTAINS
  ! ############################################################################################################################# !
  ! INITIAL FREQUENCY ESTIMATION
  ! (FUNCTION) FREQUENCY_INITIAL_(<RANGE_MIN>, <RANGE_MAX>, <PEAK>, <LENGTH>, <PAD>, <SEQUENCE>)
  ! <RANGE_MIN>            -- (IN)     (MIN) FREQUENCY RANGE (RK)
  ! <RANGE_MAX>            -- (IN)     (MAX) FREQUENCY RANGE (RK)
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = N > 0 USE N'TH PEAK WITHIN GIVEN FREQUENCY RANGE
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK)
  ! <PAD>                  -- (IN)     PADDED SEQUENCE LENGTH (IK), IF PAD > LENGTH, INPUT SEQUENCE IS PADDED WITH ZEROS
  ! <SEQUENCE>             -- (IN)     INPUT (PROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    INITIAL FREQUENCY ESTIMATION (RK)
  ! double  frequency_initial_(double* range_min, double* range_max, int* peak, int* length, int* pad, double* sequence) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_INITIAL_(RANGE_MIN, RANGE_MAX, PEAK, LENGTH, PAD, SEQUENCE) &
    BIND(C, NAME = "frequency_initial_")
    REAL(RK), INTENT(IN) :: RANGE_MIN
    REAL(RK), INTENT(IN) :: RANGE_MAX
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN):: LENGTH
    INTEGER(IK), INTENT(IN):: PAD
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    INTEGER(IK) :: BIN_MIN
    INTEGER(IK) :: BIN_MAX
    REAL(RK), DIMENSION(2_IK*PAD) :: FOURIER
    INTEGER(IK) :: BIN
    REAL(RK), DIMENSION(PAD) :: AMPLITUDE
    CALL PAD_(LENGTH, PAD, SEQUENCE, FOURIER)
    CALL __FFT__(PAD, FFT_FORWARD, FOURIER)
    AMPLITUDE = LOG10(EPSILON+SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK))
    BIN_MIN = INT(RANGE_MIN*REAL(PAD, RK), IK) + 1_IK
    BIN_MAX = INT(RANGE_MAX*REAL(PAD, RK), IK) + 0_IK
    IF (PEAK == 0_IK) THEN
      BIN = BIN_MIN-1_IK+INT(__MAXLOC__(AMPLITUDE(BIN_MIN:BIN_MAX:1_IK), 1_IK), IK)
    ELSE
      BIN = BIN_MIN-1_IK+PEAK_(BIN_MAX-BIN_MIN, AMPLITUDE(BIN_MIN:BIN_MAX:1_IK), PEAK)
    END IF
    FREQUENCY_INITIAL_ = REAL(BIN-1_IK, RK)/REAL(PAD, RK)
  END FUNCTION FREQUENCY_INITIAL_
  ! ############################################################################################################################# !
  ! INITIAL FREQUENCY ESTIMATION (MEMORIZATION)
  ! (FUNCTION) FREQUENCY_INITIAL__(<RANGE_MIN>, <RANGE_MAX>, <PEAK>, <LENGTH>, <PAD>, <SEQUENCE>)
  ! <RANGE_MIN>            -- (IN)     (MIN) FREQUENCY RANGE (RK)
  ! <RANGE_MAX>            -- (IN)     (MAX) FREQUENCY RANGE (RK)
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = N > 0 USE N'TH PEAK WITHIN GIVEN FREQUENCY RANGE
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK)
  ! <PAD>                  -- (IN)     PADDED SEQUENCE LENGTH (IK), IF PAD > LENGTH, INPUT SEQUENCE IS PADDED WITH ZEROS
  ! <SEQUENCE>             -- (IN)     INPUT (PROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    INITIAL FREQUENCY ESTIMATION (RK)
  ! double  frequency_initial__(double* range_min, double* range_max, int* peak, int* length, int* pad, double* sequence) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_INITIAL__(RANGE_MIN, RANGE_MAX, PEAK, LENGTH, PAD, SEQUENCE) &
    BIND(C, NAME = "frequency_initial__")
    REAL(RK), INTENT(IN) :: RANGE_MIN
    REAL(RK), INTENT(IN) :: RANGE_MAX
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN):: LENGTH
    INTEGER(IK), INTENT(IN):: PAD
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    INTEGER(IK) :: BIN_MIN
    INTEGER(IK) :: BIN_MAX
    REAL(RK), DIMENSION(2_IK*PAD) :: FOURIER
    INTEGER(IK) :: BIN
    REAL(RK), DIMENSION(PAD) :: AMPLITUDE
    CALL PAD_(LENGTH, PAD, SEQUENCE, FOURIER)
    CALL FFT_RADIX_EIGHT__(PAD, FFT_FORWARD, FOURIER, BANK%BIT_FFT, BANK%TRIG_FFT)
    AMPLITUDE = LOG10(EPSILON+SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK))
    BIN_MIN = INT(RANGE_MIN*REAL(PAD, RK), IK) + 1_IK
    BIN_MAX = INT(RANGE_MAX*REAL(PAD, RK), IK) + 0_IK
    IF (PEAK == 0_IK) THEN
      BIN = BIN_MIN-1_IK+INT(__MAXLOC__(AMPLITUDE(BIN_MIN:BIN_MAX:1_IK), 1_IK), IK)
    ELSE
      BIN = BIN_MIN-1_IK+PEAK_(BIN_MAX-BIN_MIN, AMPLITUDE(BIN_MIN:BIN_MAX:1_IK), PEAK)
    END IF
    FREQUENCY_INITIAL__ = REAL(BIN-1_IK, RK)/REAL(PAD, RK)
  END FUNCTION FREQUENCY_INITIAL__
  ! ############################################################################################################################# !
  ! REFINE FREQUENCY ESTIMATION (FFRFT)
  ! (FUNCTION) FREQUENCY_REFINE_(<METHOD>, <LENGTH>, <SEQUENCE>, <INITIAL>)
  ! <METHOD>               -- (IN)     METHOD
  ! <LENGTH>               -- (IN)     SEQUENCE LENGTH (IK)
  ! <SEQUENCE>             -- (IN)     INPUT (PROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <INITIAL>              -- (IN)     INITIAL FREQUENCY GUESS (RK)
  ! <FREQUENCY_REFINE_>    -- (OUT)    REFINED FREQUENCY ESTIMATION (RK)
  ! double  frequency_refine_(int* method, int* length, double* sequence, double* initial) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_REFINE_(METHOD, LENGTH, SEQUENCE, INITIAL) &
    BIND(C, NAME = "frequency_refine_")
    INTEGER(IK), INTENT(IN):: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), INTENT(IN) :: INITIAL
    REAL(RK), DIMENSION(2_IK*LENGTH) :: FOURIER
    INTEGER(IK) :: FST, CND
    REAL(RK) :: FACTOR
    REAL(RK), DIMENSION(LENGTH) :: MUL, COS_MUL, SIN_MUL
    INTEGER :: I
    FREQUENCY_REFINE_ = 0.0_RK
    FST = INT(REAL(LENGTH, RK)*INITIAL, IK)+1_IK
    FACTOR = TWO_PI*REAL(FST-2_IK, RK)/REAL(LENGTH, RK)
    MUL = FACTOR*REAL([(I, I = 0_IK, LENGTH-1_IK, 1_IK)], RK)
    COS_MUL = COS(MUL)
    SIN_MUL = SIN(MUL)
    FOURIER(1_IK::2_IK) = SEQUENCE(1_IK::2_IK)*COS_MUL-SEQUENCE(2_IK::2_IK)*SIN_MUL
    FOURIER(2_IK::2_IK) = SEQUENCE(1_IK::2_IK)*SIN_MUL+SEQUENCE(2_IK::2_IK)*COS_MUL
    CALL FFRFT_(LENGTH, 2.0_RK/REAL(LENGTH, RK), FOURIER)
    MUL = LOG10(SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK)+EPSILON)
    CND = INT(__MAXLOC__(MUL, 1_IK), IK)
    IF (METHOD == FREQUENCY_FFRFT) THEN
      FREQUENCY_REFINE_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(REAL(CND, RK)-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA) THEN
      FREQUENCY_REFINE_ = REAL(CND, RK)-0.5_RK+(MUL(-1_IK+CND)-MUL(CND))/(MUL(-1_IK+CND)-2.0_RK*MUL(CND)+MUL(1_IK+CND))
      FREQUENCY_REFINE_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINE_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA_FIT) THEN
      BLOCK
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: X
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: Y
        REAL(RK) :: A, B, C
        INTEGER(IK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: INDEX
        INDEX = CND + [(I, I = -PARABOLA_FIT_LENGTH, +PARABOLA_FIT_LENGTH, 1_IK)]
        X = REAL(INDEX, RK)
        Y = MUL([INDEX])
        CALL FIT_PARABOLA_(2_IK*PARABOLA_FIT_LENGTH+1_IK, X, Y, A, B, C, FREQUENCY_REFINE_)
        FREQUENCY_REFINE_ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINE_-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      END BLOCK
    END IF
  END FUNCTION FREQUENCY_REFINE_
  ! ############################################################################################################################# !
  ! REFINE FREQUENCY ESTIMATION (FFRFT) (MEMORIZATION)
  ! (FUNCTION) FREQUENCY_REFINE__(<METHOD>, <LENGTH>, <SEQUENCE>, <INITIAL>)
  ! <METHOD>               -- (IN)     METHOD
  ! <LENGTH>               -- (IN)     SEQUENCE LENGTH (IK)
  ! <SEQUENCE>             -- (IN)     INPUT (PROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <INITIAL>              -- (IN)     INITIAL FREQUENCY GUESS (RK)
  ! <FREQUENCY_REFINE_>    -- (OUT)    REFINED FREQUENCY ESTIMATION (RK)
  ! double  frequency_refine__(int* method, int* length, double* sequence, double* initial) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_REFINE__(METHOD, LENGTH, SEQUENCE, INITIAL) &
    BIND(C, NAME = "frequency_refine__")
    INTEGER(IK), INTENT(IN):: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), INTENT(IN) :: INITIAL
    REAL(RK), DIMENSION(2_IK*LENGTH) :: FOURIER
    INTEGER(IK) :: FST, CND
    REAL(RK) :: FACTOR
    REAL(RK), DIMENSION(LENGTH) :: MUL, COS_MUL, SIN_MUL
    INTEGER :: I
    FREQUENCY_REFINE__ = 0.0_RK
    FST = INT(REAL(LENGTH, RK)*INITIAL, IK)+1_IK
    FACTOR = TWO_PI*REAL(FST-2_IK, RK)/REAL(LENGTH, RK)
    MUL = FACTOR*REAL([(I, I = 0_IK, LENGTH-1_IK, 1_IK)], RK)
    COS_MUL = COS(MUL)
    SIN_MUL = SIN(MUL)
    FOURIER(1_IK::2_IK) = SEQUENCE(1_IK::2_IK)*COS_MUL-SEQUENCE(2_IK::2_IK)*SIN_MUL
    FOURIER(2_IK::2_IK) = SEQUENCE(1_IK::2_IK)*SIN_MUL+SEQUENCE(2_IK::2_IK)*COS_MUL
    CALL FFRFT__(LENGTH, FOURIER, BANK%BIT_FFRFT, BANK%TRIG_FFRFT, BANK%COS_FST, BANK%SIN_FST, BANK%COS_LST, BANK%SIN_LST)
    MUL = LOG10(SQRT(FOURIER(1_IK::2_IK)**2_IK+FOURIER(2_IK::2_IK)**2_IK)+EPSILON)
    CND = INT(__MAXLOC__(MUL, 1_IK), IK)
    IF (METHOD == FREQUENCY_FFRFT) THEN
      FREQUENCY_REFINE__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(REAL(CND, RK)-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA) THEN
      FREQUENCY_REFINE__ = REAL(CND, RK)-0.5_RK+(MUL(-1_IK+CND)-MUL(CND))/(MUL(-1_IK+CND)-2.0_RK*MUL(CND)+MUL(1_IK+CND))
      FREQUENCY_REFINE__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINE__-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_PARABOLA_FIT) THEN
      BLOCK
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: X
        REAL(RK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: Y
        REAL(RK) :: A, B, C
        INTEGER(IK), DIMENSION(2_IK*PARABOLA_FIT_LENGTH+1_IK) :: INDEX
        INDEX = CND + [(I, I = -PARABOLA_FIT_LENGTH, +PARABOLA_FIT_LENGTH, 1_IK)]
        X = REAL(INDEX, RK)
        Y = MUL([INDEX])
        CALL FIT_PARABOLA_(2_IK*PARABOLA_FIT_LENGTH+1_IK, X, Y, A, B, C, FREQUENCY_REFINE__)
        FREQUENCY_REFINE__ = (REAL(FST, RK)-2.0_RK+2.0_RK*(FREQUENCY_REFINE__-1.0_RK)/REAL(LENGTH, RK))/REAL(LENGTH, RK)
      END BLOCK
    END IF
  END FUNCTION FREQUENCY_REFINE__
  ! ############################################################################################################################# !
  ! REFINE FREQUENCY ESTIMATION (BINARY SEARCH)
  ! (FUNCTION) BINARY_AMPLITUDE_(<FLAG>, <LENGTH>, <TOTAL>, <WINDOW>, <SEQUENCE>, <INITIAL>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <LENGTH>               -- (IN)     SEQUENCE LENGTH (IK)
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT (UNPROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <INITIAL>              -- (IN)     INITIAL FREQUENCY GUESS (RK)
  ! <BINARY_AMPLITUDE_>    -- (OUT)    REFINED FREQUENCY (RK)
  ! double  binary_amplitude_(int* flag, int* length, double* total, double* window, double* sequence, double* initial) ;
  MODULE REAL(RK) FUNCTION BINARY_AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, INITIAL) &
    BIND(C, NAME = "binary_amplitude_")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), INTENT(IN) :: INITIAL
    BINARY_AMPLITUDE_ = BINARY_(SEARCH_, INITIAL, 1.0_RK/REAL(LENGTH, RK), SEARCH_LIMIT, SEARCH_TOLERANCE)
  CONTAINS
    REAL(RK) FUNCTION SEARCH_(FREQUENCY)
      REAL(RK), INTENT(IN) :: FREQUENCY
      REAL(RK) :: COS_AMP
      REAL(RK) :: SIN_AMP
      REAL(RK) :: AMPLITUDE
      CALL AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, FREQUENCY, COS_AMP, SIN_AMP, AMPLITUDE)
      SEARCH_ = AMPLITUDE
    END FUNCTION SEARCH_
  END FUNCTION
  ! ############################################################################################################################# !
  ! REFINE FREQUENCY ESTIMATION (GOLDEN SEARCH)
  ! (FUNCTION) GOLDEN_AMPLITUDE_(<FLAG>, <LENGTH>, <TOTAL>, <WINDOW>, <SEQUENCE>, <INITIAL>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <LENGTH>               -- (IN)     SEQUENCE LENGTH (IK)
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT (UNPROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <INITIAL>              -- (IN)     INITIAL FREQUENCY GUESS (RK)
  ! <GOLDEN_AMPLITUDE_>    -- (OUT)    REFINED FREQUENCY (RK)
  ! double  golden_amplitude_(int* flag, int* length, double* total, double* window, double* sequence, double* initial) ;
  MODULE REAL(RK) FUNCTION GOLDEN_AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, INITIAL) &
    BIND(C, NAME = "golden_amplitude_")
    INTEGER(IK), INTENT(IN):: FLAG
    INTEGER(IK), INTENT(IN):: LENGTH
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), INTENT(IN) :: INITIAL
    GOLDEN_AMPLITUDE_ = GOLDEN_(SEARCH_, INITIAL, 1.0_RK/REAL(LENGTH, RK), SEARCH_LIMIT, SEARCH_TOLERANCE)
  CONTAINS
    REAL(RK) FUNCTION SEARCH_(FREQUENCY)
      REAL(RK), INTENT(IN) :: FREQUENCY
      REAL(RK) :: COS_AMP
      REAL(RK) :: SIN_AMP
      REAL(RK) :: AMPLITUDE
      CALL AMPLITUDE_(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, FREQUENCY, COS_AMP, SIN_AMP, AMPLITUDE)
      SEARCH_ = AMPLITUDE
    END FUNCTION SEARCH_
  END FUNCTION
  ! ############################################################################################################################# !
  ! FREQUENCY ESTIMATION (GERERIC)
  ! (FUNCTION) FREQUENCY_(<FLAG>, <RANGE_MIN>, <RANGE_MAX>, <PEAK>, <METHOD>, <LENGTH>, <PAD>, <TOTAL>, <WINDOW>, <SEQUENCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <RANGE_MIN>            -- (IN)     (MIN) FREQUENCY RANGE (RK)
  ! <RANGE_MAX>            -- (IN)     (MAX) FREQUENCY RANGE (RK)
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = N > 0 USE N'TH PEAK WITHIN GIVEN FREQUENCY RANGE
  ! <METHOD>               -- (IN)     FREQUENCY ESTIMATION METHOD (IK)
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK)
  ! <PAD>                  -- (IN)     PADDED SEQUENCE LENGTH (IK), IF PAD > LENGTH, INPUT SEQUENCE IS PADDED WITH ZEROS
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT (UNPROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    FREQUENCY ESTIMATION (RK)
  ! double  frequency_(int* flag, double* range_min, double* range_max, int* peak, int* method, int* length, int* pad, double* total, double* window, double* sequence) ;
  MODULE REAL(RK) FUNCTION FREQUENCY_(FLAG, RANGE_MIN, RANGE_MAX, PEAK, METHOD, LENGTH, PAD, TOTAL, WINDOW, SEQUENCE) &
    BIND(C, NAME = "frequency_")
    INTEGER(IK), INTENT(IN):: FLAG
    REAL(RK), INTENT(IN) :: RANGE_MIN
    REAL(RK), INTENT(IN) :: RANGE_MAX
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN) :: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    INTEGER(IK), INTENT(IN):: PAD
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: COPY
    REAL(RK), DIMENSION(2_IK*LENGTH) :: LOCAL
    CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SEQUENCE, COPY)
    CALL APPLY_WINDOW_(LENGTH, WINDOW, COPY, LOCAL)
    FREQUENCY_ = FREQUENCY_INITIAL_(RANGE_MIN, RANGE_MAX, PEAK, LENGTH, PAD, LOCAL)
    IF (METHOD == FREQUENCY_FFT) RETURN
    IF (METHOD == FREQUENCY_FFRFT .OR. METHOD == FREQUENCY_PARABOLA .OR. METHOD == FREQUENCY_PARABOLA_FIT) THEN
      FREQUENCY_ = FREQUENCY_REFINE_(METHOD, LENGTH, LOCAL, FREQUENCY_)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_SEARCH) THEN
      FREQUENCY_ = __SEARCH__(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, FREQUENCY_)
      RETURN
    END IF
  END FUNCTION FREQUENCY_
  ! ############################################################################################################################# !
  ! FREQUENCY ESTIMATION (GERERIC) (MEMORIZATION)
  ! (FUNCTION) FREQUENCY__(<FLAG>, <RANGE_MIN>, <RANGE_MAX>, <PEAK>, <METHOD>, <LENGTH>, <PAD>, <TOTAL>, <WINDOW>, <SEQUENCE>)
  ! <FLAG>                 -- (IN)     COMPLEX FLAG (IK), 0/1 FOR REAL/COMPLEX INPUT SEQUENCE
  ! <RANGE_MIN>            -- (IN)     (MIN) FREQUENCY RANGE (RK)
  ! <RANGE_MAX>            -- (IN)     (MAX) FREQUENCY RANGE (RK)
  ! <PEAK>                 -- (IN)     PEAK NUMBER TO USE (IK), <PEAK> = 0 USE MAXIMUM BIN, <PEAK> = N > 0 USE N'TH PEAK WITHIN GIVEN FREQUENCY RANGE
  ! <METHOD>               -- (IN)     FREQUENCY ESTIMATION METHOD (IK)
  ! <LENGTH>               -- (IN)     INPUT SEQUENCE LENGTH (IK)
  ! <PAD>                  -- (IN)     PADDED SEQUENCE LENGTH (IK), IF PAD > LENGTH, INPUT SEQUENCE IS PADDED WITH ZEROS
  ! <TOTAL>                -- (IN)     SUM(WINDOW) (RK)
  ! <WINDOW>               -- (IN)     WINDOW ARRAY (RK ARRAY OF LENGTH = <LENGTH>)
  ! <SEQUENCE>             -- (IN)     INPUT (UNPROCESSED) SEQUENCE (RK ARRAY OF LENGTH = 2_IK*<LENGTH>), <SEQUENCE> = [..., SR_I, SI_I, ...]
  ! <FREQUENCY_>           -- (OUT)    FREQUENCY ESTIMATION (RK)
  ! double  frequency__(int* flag, double* range_min, double* range_max, int* peak, int* method, int* length, int* pad, double* total, double* window, double* sequence) ;
  MODULE REAL(RK) FUNCTION FREQUENCY__(FLAG, RANGE_MIN, RANGE_MAX, PEAK, METHOD, LENGTH, PAD, TOTAL, WINDOW, SEQUENCE) &
    BIND(C, NAME = "frequency__")
    INTEGER(IK), INTENT(IN):: FLAG
    REAL(RK), INTENT(IN) :: RANGE_MIN
    REAL(RK), INTENT(IN) :: RANGE_MAX
    INTEGER(IK), INTENT(IN) :: PEAK
    INTEGER(IK), INTENT(IN) :: METHOD
    INTEGER(IK), INTENT(IN):: LENGTH
    INTEGER(IK), INTENT(IN):: PAD
    REAL(RK), INTENT(IN) :: TOTAL
    REAL(RK), INTENT(IN), DIMENSION(LENGTH) :: WINDOW
    REAL(RK), INTENT(IN), DIMENSION(2_IK*LENGTH) :: SEQUENCE
    REAL(RK), DIMENSION(2_IK*LENGTH) :: COPY
    REAL(RK), DIMENSION(2_IK*LENGTH) :: LOCAL
    CALL REMOVE_WINDOW_MEAN_(LENGTH, TOTAL, WINDOW, SEQUENCE, COPY)
    CALL APPLY_WINDOW_(LENGTH, WINDOW, COPY, LOCAL)
    FREQUENCY__ = FREQUENCY_INITIAL__(RANGE_MIN, RANGE_MAX, PEAK, LENGTH, PAD, LOCAL)
    IF (METHOD == FREQUENCY_FFT) RETURN
    IF (METHOD == FREQUENCY_FFRFT .OR. METHOD == FREQUENCY_PARABOLA .OR. METHOD == FREQUENCY_PARABOLA_FIT) THEN
      FREQUENCY__ = FREQUENCY_REFINE__(METHOD, LENGTH, LOCAL, FREQUENCY__)
      RETURN
    END IF
    IF (METHOD == FREQUENCY_SEARCH) THEN
      FREQUENCY__ = __SEARCH__(FLAG, LENGTH, TOTAL, WINDOW, SEQUENCE, FREQUENCY__)
      RETURN
    END IF
  END FUNCTION FREQUENCY__
  ! ############################################################################################################################# !
END SUBMODULE FREQUENCY